//
// gvsong - Builds and renders songs designed for Game Boy Advance
// by Sean Connelly (@velipso), https://sean.cm
// Project Home: https://github.com/velipso/gvsong
// SPDX-License-Identifier: 0BSD
//

.begin memset32 //(dest, data, byte count)
  .thumb
  .align 4, nop
  bx    pc // change to ARM mode
  nop
arm:
  .arm
  subs  r2, #32
  blt   last32
  stmfd sp!, {r3-r9}
  mov   r3, r1
  mov   r4, r1
  mov   r5, r1
  mov   r6, r1
  mov   r7, r1
  mov   r8, r1
  mov   r9, r1
chunk:
  stmia r0!, {r1, r3-r9}
  subs  r2, #32
  blt   done32
  stmia r0!, {r1, r3-r9}
  subs  r2, #32
  blt   done32
  stmia r0!, {r1, r3-r9}
  subs  r2, #32
  blt   done32
  stmia r0!, {r1, r3-r9}
  subs  r2, #32
  bge   chunk
done32:
  ldmfd sp!, {r3-r9}
last32:
  adds  r2, #32
  bxle  lr
last4:
  str   r1, [r0], #4
  subs  r2, #4
  bgt   last4
  bx    lr
.end

.begin memset8 //(dest, data, byte count)
  .thumb
  cmp   r2, #0
  ble   return
nextByte:
  strb  r1, [r0]
  adds  r0, #1
  subs  r2, #1
  bne   nextByte
return:
  bx    lr
  .pool
.end
