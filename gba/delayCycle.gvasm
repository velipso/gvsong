//
// gvsong - Builds and renders songs designed for Game Boy Advance
// by Sean Connelly (@velipso), https://sean.cm
// Project Home: https://github.com/velipso/gvsong
// SPDX-License-Identifier: 0BSD
//

// minimum 17 cycle wait, must be in IWRAM, should be called via:
//   ...A...
//   mov   r0, rX           \__ inserts exactly r0 cycles
//   bl    @delayCycle      /   (including mov and bl)
//   ...B...
// if the delay can be stored as a mov instruction, it will work
//   ...A...
//   mov   r0, #0x100       \__ inserts delay of 0x100 cycles
//   bl    @delayCycle      /
//   ...B...
// but if you need a PC-relative load, then subtract 2 more cycles
// to do the load
//   ...A...
//   ldr   r0, =0x101       \__ inserts delay of 0x103 cycles due
//   bl    @delayCycle      /   to PC relative load
//   ...B...
.begin delayCycle //(cyclesToWait)
  .arm
  ldr   r2, =+
  sub   r0, #17
  and   r1, r0, #7
  sub   r2, r2, r1, lsl #2
  movs  r0, r0, lsr #3
- bxeq  r2
  nop
  nop
  nop
  subs  r0, #1
  b     -
  nop
  nop
  nop
  nop
  nop
  nop
  nop
+ bx    lr
  .pool
.end
